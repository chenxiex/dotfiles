# Lines configured by zsh-newuser-install
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
setopt nonomatch
bindkey -v
# End of lines configured by zsh-newuser-install
# The following lines were added by compinstall
zstyle :compinstall filename '$HOME/.zshrc'

autoload -Uz compinit
compinit
# End of lines added by compinstall

### plugins
_PLUGINS_DIR="${XDG_DATA_HOME:-$HOME/.local/share}/zsh/plugins"
[ -d "$_PLUGINS_DIR" ] || mkdir -p "$_PLUGINS_DIR"
plugin() {
    local repo_url="$1"
    local entry="$2"

    if [[ "$repo_url" != http*://* ]]; then
        repo_url="https://github.com/$repo_url"
    fi

    local name="${repo_url##*/}"
    local dir="$_PLUGINS_DIR/$name"

    if [ ! -d "$dir" ]; then
        git clone "$repo_url" "$dir"
    fi

    if [ -z "$entry" ]; then
        local zsh_files=("$dir"/*.plugin.zsh(N))
        if (( ${#zsh_files[@]} == 1 )); then
            entry="${zsh_files[1]##*/}"
		else
			echo "Error: cannot determine entry for plugin '$name' in '$dir'" >&2
			echo " Please specify entry explicitly as second argument." >&2
			return 1
        fi
    fi

    if [ ! -f "$dir/$entry" ]; then
        echo "Plugin '$name' entry '$entry' missing, repairing…"
        rm -rf "$dir"
        git clone "$repo_url" "$dir"
    fi

    if [ -f "$dir/$entry" ]; then
        source "$dir/$entry"
    else
        echo "Error: plugin '$name' entry '$entry' still missing after repair."
    fi
}
update_plugins() {
    local dir

    [ -d "$_PLUGINS_DIR" ] || {
        echo "Plugin directory '$_PLUGINS_DIR' does not exist."
        return 1
    }

    echo "Updating plugins in $_PLUGINS_DIR"

    for dir in "$_PLUGINS_DIR"/*; do
        [ -d "$dir" ] || continue

        if [ -d "$dir/.git" ]; then
            echo "→ Updating $(basename "$dir")"
            git -C "$dir" pull --ff-only
        else
            echo "→ Skipping $(basename "$dir") (not a git repo)"
        fi
    done
}
ZSH_AUTOSUGGEST_STRATEGY=(history completion)
plugin zsh-users/zsh-autosuggestions
plugin zdharma-continuum/fast-syntax-highlighting
### End of plugins

### Define key
typeset -g -A key

key[Home]="${terminfo[khome]}"
key[End]="${terminfo[kend]}"
key[Insert]="${terminfo[kich1]}"
key[Backspace]="${terminfo[kbs]}"
key[Delete]="${terminfo[kdch1]}"
key[Up]="${terminfo[kcuu1]}"
key[Down]="${terminfo[kcud1]}"
key[Left]="${terminfo[kcub1]}"
key[Right]="${terminfo[kcuf1]}"
key[PageUp]="${terminfo[kpp]}"
key[PageDown]="${terminfo[knp]}"
key[Shift-Tab]="${terminfo[kcbt]}"

[[ -n "${key[Home]}"      ]] && bindkey -- "${key[Home]}"       beginning-of-line
[[ -n "${key[End]}"       ]] && bindkey -- "${key[End]}"        end-of-line
[[ -n "${key[Insert]}"    ]] && bindkey -- "${key[Insert]}"     overwrite-mode
[[ -n "${key[Backspace]}" ]] && bindkey -- "${key[Backspace]}"  backward-delete-char
[[ -n "${key[Delete]}"    ]] && bindkey -- "${key[Delete]}"     delete-char
[[ -n "${key[Up]}"        ]] && bindkey -- "${key[Up]}"         up-line-or-history
[[ -n "${key[Down]}"      ]] && bindkey -- "${key[Down]}"       down-line-or-history
[[ -n "${key[Left]}"      ]] && bindkey -- "${key[Left]}"       backward-char
[[ -n "${key[Right]}"     ]] && bindkey -- "${key[Right]}"      forward-char
[[ -n "${key[PageUp]}"    ]] && bindkey -- "${key[PageUp]}"     beginning-of-buffer-or-history
[[ -n "${key[PageDown]}"  ]] && bindkey -- "${key[PageDown]}"   end-of-buffer-or-history
[[ -n "${key[Shift-Tab]}" ]] && bindkey -- "${key[Shift-Tab]}"  reverse-menu-complete

if (( ${+terminfo[smkx]} && ${+terminfo[rmkx]} )); then
	autoload -Uz add-zle-hook-widget
	function zle_application_mode_start { echoti smkx }
	function zle_application_mode_stop { echoti rmkx }
	add-zle-hook-widget -Uz zle-line-init zle_application_mode_start
	add-zle-hook-widget -Uz zle-line-finish zle_application_mode_stop
fi
###

### History
autoload -Uz up-line-or-beginning-search down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search

[[ -n "${key[PageUp]}"   ]] && bindkey -- "${key[PageUp]}"   up-line-or-beginning-search
[[ -n "${key[PageDown]}"   ]] && bindkey -- "${key[PageDown]}" down-line-or-beginning-search
###

### completion
setopt completealiases
zstyle ':completion:*' menu yes select search
autoload -U +X bashcompinit && bashcompinit
###

### env
ENV_PATH="$HOME/.sh_shared/env"
if [[ -e "$ENV_PATH" ]]; then
	export $(envsubst < "$ENV_PATH")
fi
###

### aliases
ALIAS_PATH="$HOME/.sh_shared/alias"
if [[ -e "$ALIAS_PATH" ]]; then
	source "$ALIAS_PATH"
fi
### End of aliases

### functions
FUNC_PATH="$HOME/.sh_shared/func"
if [[ -e "$FUNC_PATH" ]]; then
	source "$FUNC_PATH"
fi
### End of functions

### Prompt
autoload -Uz add-zsh-hook
autoload -Uz colors
colors

### Vi mode indicator
_VI_SYM=">"

function zle-line-init zle-keymap-select {
    case $KEYMAP in
        vicmd)  _VI_SYM="<" ;;
        viins|main) _VI_SYM=">" ;;
    esac
    zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select
### End of Vi mode indicator

_PROMPT_USERHOST=
_PROMPT_COLOR="green"
function _update_prompt_env() {
    local ret=$?

    if [[ -n $SSH_CONNECTION ]] \
       || [[ -f /run/.containerenv ]] \
       || grep -qE '(docker|kubepods|containerd)' /proc/1/cgroup 2>/dev/null
    then
        _PROMPT_USERHOST="%n@%m " 
    else
        _PROMPT_USERHOST= 
    fi

	if (( EUID == 0 )); then
		if (( ret == 0 )); then
			_PROMPT_COLOR="cyan"
		else
			_PROMPT_COLOR="magenta"
		fi
	else
		if (( ret == 0 )); then
			_PROMPT_COLOR="green"
		else
			_PROMPT_COLOR="red"
		fi
	fi
}

add-zsh-hook precmd _update_prompt_env

setopt PROMPT_SUBST

PROMPT='
[${_PROMPT_USERHOST}%F{blue}%~%f]
%F{${_PROMPT_COLOR}}${_VI_SYM}%f '
### End of prompt

# Command-not-found
if [ -e "/usr/share/doc/pkgfile/command-not-found.zsh" ]; then
    source /usr/share/doc/pkgfile/command-not-found.zsh
fi
# End of command-not-found

### conda
if command -v conda >/dev/null 2>&1; then
# >>> conda initialize >>>
# !! Contents within this block are managed by 'conda init' !!
__conda_setup="$('/opt/conda/bin/conda' 'shell.zsh' 'hook' 2> /dev/null)"
if [ $? -eq 0 ]; then
    eval "$__conda_setup"
else
    if [ -f "/opt/conda/etc/profile.d/conda.sh" ]; then
        . "/opt/conda/etc/profile.d/conda.sh"
    else
        export PATH="/opt/conda/bin:$PATH"
    fi
fi
unset __conda_setup
# <<< conda initialize <<<
fi
### End of conda

