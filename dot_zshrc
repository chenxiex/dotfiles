# Lines configured by zsh-newuser-install
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
setopt nonomatch
bindkey -v
# End of lines configured by zsh-newuser-install
# The following lines were added by compinstall
zstyle :compinstall filename '$HOME/.zshrc'

autoload -Uz compinit
compinit
# End of lines added by compinstall

### plugins
_PLUGINS_DIR="${XDG_DATA_HOME:-$HOME/.local/share}/zsh/plugins"
[ -d "$_PLUGINS_DIR" ] || mkdir -p "$_PLUGINS_DIR"
[ -d "$_PLUGINS_DIR"/zsh-autosuggestions ] || git clone https://github.com/zsh-users/zsh-autosuggestions "$_PLUGINS_DIR"/zsh-autosuggestions 
[ -d "$_PLUGINS_DIR"/fast-syntax-highlighting ] || git clone https://github.com/zdharma-continuum/fast-syntax-highlighting "$_PLUGINS_DIR"/fast-syntax-highlighting
ZSH_AUTOSUGGEST_STRATEGY=(history completion)
source "$_PLUGINS_DIR"/zsh-autosuggestions/zsh-autosuggestions.zsh
source "$_PLUGINS_DIR"/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh
### End of plugins

### Define key
typeset -g -A key

key[Home]="${terminfo[khome]}"
key[End]="${terminfo[kend]}"
key[Insert]="${terminfo[kich1]}"
key[Backspace]="${terminfo[kbs]}"
key[Delete]="${terminfo[kdch1]}"
key[Up]="${terminfo[kcuu1]}"
key[Down]="${terminfo[kcud1]}"
key[Left]="${terminfo[kcub1]}"
key[Right]="${terminfo[kcuf1]}"
key[PageUp]="${terminfo[kpp]}"
key[PageDown]="${terminfo[knp]}"
key[Shift-Tab]="${terminfo[kcbt]}"

[[ -n "${key[Home]}"      ]] && bindkey -- "${key[Home]}"       beginning-of-line
[[ -n "${key[End]}"       ]] && bindkey -- "${key[End]}"        end-of-line
[[ -n "${key[Insert]}"    ]] && bindkey -- "${key[Insert]}"     overwrite-mode
[[ -n "${key[Backspace]}" ]] && bindkey -- "${key[Backspace]}"  backward-delete-char
[[ -n "${key[Delete]}"    ]] && bindkey -- "${key[Delete]}"     delete-char
[[ -n "${key[Up]}"        ]] && bindkey -- "${key[Up]}"         up-line-or-history
[[ -n "${key[Down]}"      ]] && bindkey -- "${key[Down]}"       down-line-or-history
[[ -n "${key[Left]}"      ]] && bindkey -- "${key[Left]}"       backward-char
[[ -n "${key[Right]}"     ]] && bindkey -- "${key[Right]}"      forward-char
[[ -n "${key[PageUp]}"    ]] && bindkey -- "${key[PageUp]}"     beginning-of-buffer-or-history
[[ -n "${key[PageDown]}"  ]] && bindkey -- "${key[PageDown]}"   end-of-buffer-or-history
[[ -n "${key[Shift-Tab]}" ]] && bindkey -- "${key[Shift-Tab]}"  reverse-menu-complete

if (( ${+terminfo[smkx]} && ${+terminfo[rmkx]} )); then
	autoload -Uz add-zle-hook-widget
	function zle_application_mode_start { echoti smkx }
	function zle_application_mode_stop { echoti rmkx }
	add-zle-hook-widget -Uz zle-line-init zle_application_mode_start
	add-zle-hook-widget -Uz zle-line-finish zle_application_mode_stop
fi
###

### History
autoload -Uz up-line-or-beginning-search down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search

[[ -n "${key[PageUp]}"   ]] && bindkey -- "${key[PageUp]}"   up-line-or-beginning-search
[[ -n "${key[PageDown]}"   ]] && bindkey -- "${key[PageDown]}" down-line-or-beginning-search
###

### completion
setopt completealiases
zstyle ':completion:*' menu yes select search
autoload -U +X bashcompinit && bashcompinit
###

### env
ENV_PATH="$HOME/.sh_shared/env"
if [[ -e "$ENV_PATH" ]]; then
	export $(envsubst < "$ENV_PATH")
fi
###

### aliases
ALIAS_PATH="$HOME/.sh_shared/alias"
if [[ -e "$ALIAS_PATH" ]]; then
	source "$ALIAS_PATH"
fi
### End of aliases

### functions
FUNC_PATH="$HOME/.sh_shared/func"
if [[ -e "$FUNC_PATH" ]]; then
	source "$FUNC_PATH"
fi
### End of functions

### Prompt
autoload -Uz add-zsh-hook

_prompt_shown=0
_show_userhost=

function _update_prompt_env() {
    local ret=$?

    # 首次不换行，之后每次命令前换行
    if (( _prompt_shown )); then
        print
    else
        _prompt_shown=1
    fi

    # 判断是否显示 user@host
    if [[ -n $SSH_CONNECTION ]] \
       || [[ -f /run/.containerenv ]] \
       || grep -qE '(docker|kubepods|containerd)' /proc/1/cgroup 2>/dev/null
    then
        _show_userhost=1
    else
        _show_userhost=
    fi

    # 保存上一条命令返回值
    PROMPT_RET=$ret
}

add-zsh-hook precmd _update_prompt_env

# ---------- Prompt ----------
setopt PROMPT_SUBST

PROMPT='$(

    if [[ -n $_show_userhost ]]; then
        print -n "[%n@%m %F{blue}%~%f]"
    else
        print -n "[%F{blue}%~%f]"
    fi

    print

    if (( PROMPT_RET == 0 )); then
        print -n "%F{green}%#%f "
    else
        print -n "%F{red}%#%f "
    fi
)'
### End of prompt

# Command-not-found
if [ -e "/usr/share/doc/pkgfile/command-not-found.zsh" ]; then
    source /usr/share/doc/pkgfile/command-not-found.zsh
fi
# End of command-not-found

### conda
if command -v conda >/dev/null 2>&1; then
# >>> conda initialize >>>
# !! Contents within this block are managed by 'conda init' !!
__conda_setup="$('/opt/conda/bin/conda' 'shell.zsh' 'hook' 2> /dev/null)"
if [ $? -eq 0 ]; then
    eval "$__conda_setup"
else
    if [ -f "/opt/conda/etc/profile.d/conda.sh" ]; then
        . "/opt/conda/etc/profile.d/conda.sh"
    else
        export PATH="/opt/conda/bin:$PATH"
    fi
fi
unset __conda_setup
# <<< conda initialize <<<
fi
### End of conda

